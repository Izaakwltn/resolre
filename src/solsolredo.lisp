;;;; src/solsolredo.lisp (brainfuck)
;;;;
;;;; Copyright Izaak Walton (c) 2023

(in-package :resolre)

;;; functions for converting to and from brainfuck

;;; Since Resolre only has 7 commands, ommitting char-input, converting from resolre
;;; to brainfuck works all the time but converting the other only works without ','
(defvar *rsr-bf-table* '(("do" ">")
                         ("re" "<")
                         ("mi" "+")
                         ("fa" "-")
                         ("so" ".")
                         ("la" "[")
                         ("si" "]")))

;;; converting resolre to brainfuck

(defun convert-to-bf (command)
  (second (assoc command *rsr-bf-table* :test #'string-equal)))

(defun convert-commands-to-bf (commands-list)
  (mapcar #'convert-to-bf commands-list))

(defun convert-string-to-bf (rsr-string)
  (format nil "狺ㄣ镱鲥螋泔眄犷潴麸怄疳蝮蝮颦篝蜷铉┅┅ㄤ彐躅泔铞弪舡骈戾麸怄蝮颦骈戾疳翳怄骈戾疳翳戾è泔眄犷潴ㄣ镱鲥螋泔眄犷潴麸怄疳蝮瀛骈戾蝮颦骈戾疳翳┅┅鏖翳镳孱骈戾篝怄骈戾疳翳轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴洪姝滹弩铒舡屮轶恒蝈狒濠ㄦ矧磲篝狺泔眄犷潴┅┅换蔑铞弪糸铉骝镯怛衢铈蹉麸蝈箫祢ㄤ彐鲠怛衢铈蹉氕汨狎徙翦蝮Ж⒕⒓③⑤┅ㄤ彐躅疳蝮瀛怄ㄢ姝篝蜷铉祜镳瑚轸疳蝮邃航铋烘矧烘蝻呼戾铉翳怄篝蜷铉轰戾è篚怏羼怄篝蜷铉ū椹椹┅ㄣ镱è礤礅弪怛衢铈蹉氕汨狎徙翦蝮呼弩＇篝蜷铉羼踽飑箦赳疳蝮邃ㄣ镱疳蝮邃┅è篝蜷铉羼踽蝈趱蝾⑴乙弦晌孟托猎陕膛蜗遗酉桃萌烈撩耘晌姓寓┅┅烘轭犰禊蝈趱蝾蝈鲥蝮疳蝮邃┅┅ㄤ彐躅疳蝮瀛怄骈戾ㄦ殪屦狒瑭疳蝮瀛怄蹰镳候遽洵骈戾篝蜷铉骈戾疳翳┅ㄤ彐躅泔铞弪舡麸蝮ㄢ姝泔眄犷洎ㄦ轵篝ㄦ轭洵殒＇灬礅溽篝蜷铉羼踽箦泔钿怄泔眄犷洎蝮颦怄翎忪濯┅ㄤ彐躅泔铞弪舡泔眄犷潴麸蝮ㄣ镯磲钿蟓扉篝磲疸狎＇泔铞弪舡麸蝮泔眄犷潴扉篝┅ㄤ彐躅泔铞弪舡篝蜷铉麸蝮ㄢ姝篝蜷铉ㄦ矧磲铋窿ㄣ镱鲥螋泔眄犷潴麸蝮疳蝮瀛怄怄篝蜷铉┅┅ㄤ彐躅泔铞弪舡骈戾麸蝮ㄢ姝骈戾疳翳蝮颦骈戾疳翳戾è泔眄犷潴ㄣ镱鲥螋泔眄犷潴麸蝮疳蝮瀛怄骈戾怄骈戾疳翳┅┅鏖翳镳孱骈戾篝蝮颦骈戾疳翳轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴洪姝滹弩铒舡屮轶恒蝈狒濠ㄦ矧磲篝狺泔眄犷潴┅┅